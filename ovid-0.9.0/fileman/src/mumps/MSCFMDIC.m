MSCFMDIC ;
 ;
 Q
RUN(REPLY,RES) ; main entry point
 N PROPNAME,FILENUM
 ; Parse out the command properties
 F  Q:'$$NEXTPROP^MSCRES(.RES)  D
 . S PROPNAME=$$GETPROP^MSCRES(.RES)
 . I PROPNAME="FILE" S FILENUM=$$GETVAL^MSCRES(.RES) Q
 . Q
 D SCAN(.REPLY,$NA(^DD(FILENUM)),1)
 D SCAN(.REPLY,$NA(^DIC(FILENUM)),0)
 Q
SCAN(REPLY,GLOBNAME,ONLYNUM)
 N I
 F I=0:1:$QL(GLOBNAME) D
 . D ADDCMPD^MSCRES(.REPLY,"N")
 . D ADDPROP^MSCRES(.REPLY,"S",$QS(GLOBNAME,I))
 . Q
 D:$D(@GLOBNAME)#10 ADDPROP^MSCRES(.REPLY,"V",@GLOBNAME)
 D SUBSCAN(.REPLY,GLOBNAME,ONLYNUM)
 F I=0:1:$QL(GLOBNAME) D ENDCMPD^MSCRES(.REPLY)
 Q
SUBSCAN(REPLY,GLOBNAME,ONLYNUM) ; populate based on global name
 N I,GLEN,TMP
 S I=""
 S GLEN=$QL(GLOBNAME)
 F  S I=$O(@GLOBNAME@(I)) Q:(I="")!((I'=+I)&ONLYNUM)  D
 . D ADDCMPD^MSCRES(.REPLY,"N")
 . D ADDPROP^MSCRES(.REPLY,"S",I)
 . D:$D(@GLOBNAME@(I))#10 ADDPROP^MSCRES(.REPLY,"V",@GLOBNAME@(I))
 . D SUBSCAN(.REPLY,$NA(@GLOBNAME@(I)),0)
 . D ENDCMPD^MSCRES(.REPLY) ; N
 . Q
 Q
READSUB(RES,GLOBAL)
 N PROPNAME,SUB,VAL,TMP
 F  Q:'$$NEXTPROP^MSCRES(.RES)  D
 . S PROPNAME=$$GETPROP^MSCRES(.RES)
 . I PROPNAME="N" D READSUB(.RES,.TMP) M GLOBAL(SUB)=TMP Q
 . I PROPNAME="S" S SUB=$$GETVAL^MSCRES(.RES) Q
 . I PROPNAME="V" S VAL=$$GETVAL^MSCRES(.RES) Q
 . Q
 S GLOBAL(SUB)=VAL
 Q
READ(RES,GLOBAL)
 N PROPNAME,X
 S X=$$NEXTPROP^MSCRES(.RES)
 F  Q:'$$NEXTPROP^MSCRES(.RES)  D
 . S PROPNAME=$$GETPROP^MSCRES(.RES)
 . I PROPNAME="N" D READSUB(.RES,.GLOBAL) Q
 . I PROPNAME="V" S GLOBAL=$$GETVAL^MSCRES(.RES) Q
 . Q
 S X=$$NEXTPROP^MSCRES(.RES)
 Q
