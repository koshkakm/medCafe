MSCFMGET; JDA 24NOV2007
 ;
 Q
RUN(REPLY,RES)
 N PROPNAME,FILE,IENS,FIELDS,TARGET,NAMES,FLAGS,NEWFLD
 S FIELDS="",FLAGS="N"
 ; Parse out the command properties
 F  Q:'$$NEXTPROP^MSCRES(.RES)  D
 . S PROPNAME=$$GETPROP^MSCRES(.RES)
 . I PROPNAME="FILE" S FILE=$$FILE2NUM^MSCFM($$GETVAL^MSCRES(.RES)) W FILE,! Q
 . I PROPNAME="IENS" S IENS=$$GETVAL^MSCRES(.RES) Q
 . I PROPNAME="FIELD" S NEWFLD=$$PROCFLD(.RES,FILE,.NAMES) I NEWFLD'="" D  Q
 . . S:FIELDS'="" FIELDS=FIELDS_";" S FIELDS=FIELDS_NEWFLD
 . . Q
 . I PROPNAME="INTERNAL" S:$$GETVAL^MSCRES(.RES) FLAGS=FLAGS_"I" Q
 . Q
 D GETS^DIQ(FILE,IENS,FIELDS,FLAGS,"TARGET","MSG")
 N IDX,VALUE,WPROW,SFCOUNT
 D ADDCMPD^MSCRES(.REPLY,"RESULTS")
 S IDX=""
 F  S IDX=$O(TARGET(FILE,IENS,IDX)) Q:IDX=""  D
 . D ADDCMPD^MSCRES(.REPLY,"FIELD")
 . D ADDPROP^MSCRES(.REPLY,"NAME",NAMES(IDX))
 . I $D(TARGET(FILE,IENS,IDX,1))=1 D  ; Word processing field
 . . F WPROW=1:1 Q:$D(TARGET(FILE,IENS,IDX,WPROW))=0  D
 . . . D ADDPROP^MSCRES(.REPLY,"VALUE",TARGET(FILE,IENS,IDX,WPROW))
 . . . Q
 . . Q
 . E  I $D(NAMES(IDX,"TYPE")) D ; Special type - variable pointer
 . . S VALUE=$G(TARGET(FILE,IENS,IDX,"I"))
 . . ; Convert root to file number pointer to and return that
 . . S $P(VALUE,";",2)=+$P(@("^"_$P(VALUE,";",2)_"0)"),"^",2)
 . . D ADDPROP^MSCRES(.REPLY,"VALUE",VALUE)
 . . Q
 . E  D
 . . S VALUE=$G(TARGET(FILE,IENS,IDX,"I"))
 . . S:VALUE="" VALUE=TARGET(FILE,IENS,IDX)
 . . D ADDPROP^MSCRES(.REPLY,"VALUE",VALUE)
 . . Q
 . D ENDCMPD^MSCRES(.REPLY) ; FIELD
 . Q
 S IDX="" F  S IDX=$O(NAMES(IDX)) Q:IDX=""  D
 . Q:'$D(NAMES(IDX,"SUB"))
 . K TARGET,MSG
 . D LIST^DIC(NAMES(IDX,"SUB"),","_IENS,"@","Q",,,,,,,"TARGET","MSG")
 . S SFCOUNT=+$O(TARGET("DILIST",2,""),-1)
 . Q:'SFCOUNT
 . D ADDCMPD^MSCRES(.REPLY,"FIELD")
 . D ADDPROP^MSCRES(.REPLY,"NAME",NAMES(IDX))
 . D ADDPROP^MSCRES(.REPLY,"VALUE",SFCOUNT)
 . D ENDCMPD^MSCRES(.REPLY) ; FIELD
 . Q
 D ENDCMPD^MSCRES(.REPLY) ; RESULTS
 Q
PROCFLD(RES,FILE,NAMES)
 N PROPNAME,NAME,TYPE,NUM,SFNUM
 F  Q:'$$NEXTPROP^MSCRES(.RES)  D
 . S PROPNAME=$$GETPROP^MSCRES(.RES)
 . I PROPNAME="NAME" S NAME=$$GETVAL^MSCRES(.RES) Q
 . I PROPNAME="TYPE" S TYPE=$$GETVAL^MSCRES(.RES) Q
 . Q
 S NUM=$$FLD2NUM^MSCFM(FILE,NAME)
 S NAMES(NUM)=NAME
 S SFNUM=+$P(^DD(FILE,NUM,0),"^",2)
 I SFNUM S NAMES(NUM,"SUB")=SFNUM Q ""
 S:$D(TYPE) NAMES(NUM,"TYPE")=TYPE
 Q NUM
