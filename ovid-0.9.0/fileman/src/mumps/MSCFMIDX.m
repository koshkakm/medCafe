MSCFMIDX ; JDA 13JUN2008
 ;
 Q
RUN(REPLY,RES)
 N FILENUM,PROPNAME,IDXNAME,FIELD,FILE
 F  Q:'$$NEXTPROP^MSCRES(.RES)  D
 . S PROPNAME=$$GETPROP^MSCRES(.RES)
 . I PROPNAME="FILE" S FILENUM=$$GETVAL^MSCRES(.RES) Q
 . Q
 S IDXNAME=""
 ; old style indexes
 F  S IDXNAME=$O(^DD(FILENUM,0,"IX",IDXNAME)) Q:IDXNAME=""  D
 . D ADDCMPD^MSCRES(.REPLY,"INDEX")
 . D ADDPROP^MSCRES(.REPLY,"NAME",IDXNAME)
 . S FILE=$O(^DD(FILENUM,0,"IX",IDXNAME,""))
 . D ADDCMPD^MSCRES(.REPLY,"FIELD")
 . D ADDPROP^MSCRES(.REPLY,"FILE",FILE)
 . S FIELD=$O(^DD(FILENUM,0,"IX",IDXNAME,FILE,""))
 . D ADDPROP^MSCRES(.REPLY,"FIELD",FIELD)
 . D ENDCMPD^MSCRES(.REPLY) ; FIELD
 . D ENDCMPD^MSCRES(.REPLY) ; INDEX
 . Q
 ; new style indexes
 N SCREEN,TRG,IDXNUM,TRG2,SUB,MSG,IENS
 S SCREEN="I $P(^DD(""IX"",Y,0),""^"",14)'=""A"""
 D FIND^DIC(.11,,"@;.11;.02;","QX",FILENUM,,"B",SCREEN,,"TRG")
 S IDXNUM="" F  S IDXNUM=$O(TRG("DILIST","ID",IDXNUM)) Q:IDXNUM=""  D
 . D ADDCMPD^MSCRES(.REPLY,"INDEX")
 . D ADDPROP^MSCRES(.REPLY,"STYLE","New")
 . D ADDPROP^MSCRES(.REPLY,"NAME",TRG("DILIST","ID",IDXNUM,.02))
 . D ADDPROP^MSCRES(.REPLY,"SHORTDESC",TRG("DILIST","ID",IDXNUM,.11))
 . S IENS=TRG("DILIST",2,IDXNUM)_","
 . N DESC I $$GET1^DIQ(.11,IENS,.1,,"DESC","MSG")'="" D
 . . N TMP S TMP=""
 . . F  S TMP=$O(DESC(TMP)) Q:TMP=""  D ADDPROP^MSCRES(.REPLY,"LONGDESC",DESC(TMP))
 . . Q
 . D LIST^DIC(.114,","_IENS,"@;.01;2;3","Q",,,,,,,"TRG2")
 . S SUB="" F  S SUB=$O(TRG2("DILIST","ID",SUB)) Q:SUB=""  D
 . . D ADDCMPD^MSCRES(.REPLY,"FIELD")
 . . D ADDPROP^MSCRES(.REPLY,"ORDER",TRG2("DILIST","ID",SUB,.01))
 . . D ADDPROP^MSCRES(.REPLY,"FILE",TRG2("DILIST","ID",SUB,2))
 . . D ADDPROP^MSCRES(.REPLY,"FIELD",TRG2("DILIST","ID",SUB,3))
 . . D ENDCMPD^MSCRES(.REPLY) ; FIELD
 . . Q
 . D ENDCMPD^MSCRES(.REPLY) ; INDEX
 . Q
 Q
