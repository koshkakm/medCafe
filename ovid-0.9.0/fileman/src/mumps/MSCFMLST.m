MSCFMLST; JDA 13NOV2007
 ;
 Q
RUN(REPLY,RES)
 N THIS,FROM
 D CNSTRCT(.THIS)
 N TARGET
 D PROCESS(.THIS,.RES)
 I $G(THIS("ERROR"))'="" D ADDPROP^MSCRES(.REPLY,"ERROR",THIS("ERROR")) Q
 D:$G(THIS("SAFE")) SAFE(.THIS)
 I $G(THIS("ERROR"))'="" D ADDPROP^MSCRES(.REPLY,"ERROR",THIS("ERROR")) Q
 M:$D(THIS("FROM")) FROM=THIS("FROM")
 ; Call Fileman
 D LIST^DIC(THIS("FILE"),THIS("IENS"),THIS("FIELDS"),THIS("FLAGS"),THIS("NUMBER"),.FROM,,,THIS("SCREEN"),,"TARGET")
 M THIS("FROM")=FROM
 D BLDREPLY^MSCFMQRY(.THIS,.TARGET,.REPLY)
 Q
PROCESS(THIS,RES)
 F  Q:('$$NEXTPROP^MSCRES(.RES))!($G(THIS("ERROR"))'="")  D
 . D PROCPROP(.THIS,.RES)
 . Q 
 Q
PROCPROP(THIS,RES)
 D PROCPROP^MSCFMQRY(.THIS,.RES)
 Q
CNSTRCT(THIS)
 D CNSTRCT^MSCFMQRY(.THIS)
 Q
SAFE(THIS)
 N TRG,I,PCNUM,PC,FIELD,FNUM,DONE,RESULT
 S FIELDS=THIS("FIELDS")
 D FIND^DIC(21455,,"@;1;2;3I","Q",THIS("FILE"),,"C",,,"TRG")
 Q:'$P(TRG("DILIST",0),"^",1)
 S RESULT="@"
 F PCNUM=2:1:$L(FIELDS,";") D
 . S PC=$P(FIELDS,";",PCNUM)
 . S FNUM=+PC
 . S I="",DONE=0 F  S I=$O(TRG("DILIST","ID",I)) Q:I=""  D  Q:DONE
 . . I TRG("DILIST","ID",I,3)="S" S THIS("ERROR")="No data due to safe mode.",DONE=1 Q
 . . I TRG("DILIST","ID",I,2)=FNUM D  S DONE=1
 . . . Q:TRG("DILIST","ID",I,3)="N"
 . . . I TRG("DILIST","ID",I,3)="I" S:PC'["I" PC=PC_"I" S RESULT=RESULT_";"_PC Q
 . . . Q
 . S:'DONE RESULT=RESULT_";"_PC
 . Q
 S THIS("FIELDS")=RESULT
 Q
