MSCVDIC	; MSC/JDA - OVID FileMan DIC ; SEP 28, 2010 22:00:00
 ;;1.0;OpenVista Interface Domain;**1500**;May 14, 2009;
RUN(REPLY,RES)	; main entry point
 N PROPNAME,FILENUM
 ; Parse out the command properties
 F  Q:'$$NEXTPROP^MSCVRES(.RES)  D
 . S PROPNAME=$$GETPROP^MSCVRES(.RES)
 . I PROPNAME="FILE" S FILENUM=$$GETVAL^MSCVRES(.RES) Q
 . Q
 D SCAN(.REPLY,$NA(^DD(FILENUM)),1)
 D SCAN(.REPLY,$NA(^DIC(FILENUM)),0)
 Q
SCAN(REPLY,GLOBNAME,ONLYNUM)	;
 N I
 F I=0:1:$QL(GLOBNAME) D
 . D ADDCMPD^MSCVRES(.REPLY,"N")
 . D ADDPROP^MSCVRES(.REPLY,"S",$QS(GLOBNAME,I))
 . Q
 D:$D(@GLOBNAME)#10 ADDPROP^MSCVRES(.REPLY,"V",@GLOBNAME)
 D SUBSCAN(.REPLY,GLOBNAME,ONLYNUM)
 F I=0:1:$QL(GLOBNAME) D ENDCMPD^MSCVRES(.REPLY)
 Q
SUBSCAN(REPLY,GLOBNAME,ONLYNUM)	; populate based on global name
 N I,GLEN,TMP
 S I=""
 S GLEN=$QL(GLOBNAME)
 F  S I=$O(@GLOBNAME@(I)) Q:(I="")!((I'=+I)&ONLYNUM)  D
 . D ADDCMPD^MSCVRES(.REPLY,"N")
 . D ADDPROP^MSCVRES(.REPLY,"S",I)
 . D:$D(@GLOBNAME@(I))#10 ADDPROP^MSCVRES(.REPLY,"V",@GLOBNAME@(I))
 . D SUBSCAN(.REPLY,$NA(@GLOBNAME@(I)),0)
 . D ENDCMPD^MSCVRES(.REPLY) ; N
 . Q
 Q
READSUB(RES,GLOBAL)	;
 N PROPNAME,SUB,VAL,TMP
 F  Q:'$$NEXTPROP^MSCVRES(.RES)  D
 . S PROPNAME=$$GETPROP^MSCVRES(.RES)
 . I PROPNAME="N" D READSUB(.RES,.TMP) M GLOBAL(SUB)=TMP Q
 . I PROPNAME="S" S SUB=$$GETVAL^MSCVRES(.RES) Q
 . I PROPNAME="V" S VAL=$$GETVAL^MSCVRES(.RES) Q
 . Q
 S GLOBAL(SUB)=VAL
 Q
READ(RES,GLOBAL)	;
 N PROPNAME,X
 S X=$$NEXTPROP^MSCVRES(.RES)
 F  Q:'$$NEXTPROP^MSCVRES(.RES)  D
 . S PROPNAME=$$GETPROP^MSCVRES(.RES)
 . I PROPNAME="N" D READSUB(.RES,.GLOBAL) Q
 . I PROPNAME="V" S GLOBAL=$$GETVAL^MSCVRES(.RES) Q
 . Q
 S X=$$NEXTPROP^MSCVRES(.RES)
 Q
