MSCVFI	; MSC/JDA - OVID FileMan FILES ; SEP 28, 2010 22:00:00
 ;;1.0;OpenVista Interface Domain;**1500**;May 14, 2009
FILEINFO(REPLY,RES)	;
 N PROPNAME,FILE,FILENUM,SQL
 S SQL=0
 ; Parse out the command properties
 F  Q:'$$NEXTPROP^MSCVRES(.RES)  D
 . S PROPNAME=$$GETPROP^MSCVRES(.RES)
 . I PROPNAME="FILE" S FILE=$$GETVAL^MSCVRES(.RES) Q
 . I PROPNAME="SQL" S SQL=$$GETVAL^MSCVRES(.RES) Q
 . Q
 I FILE=+FILE S FILENUM=FILE S FILE=$$NUM2FILE^MSCV(FILENUM)
 E  S FILENUM=$$FILE2NUM^MSCV(FILE)
 I (FILENUM=0)!(FILE="")!('$$VFILE^DILFD(FILENUM)) D ADDPROP^MSCVRES(.REPLY,"ERROR","Could not find file.") D ENDCMPD^MSCVRES(.REPLY) Q
 ; If this is a subfile, go to top file
 F  Q:$G(^DD(FILENUM,0,"UP"))=""  S FILENUM=^DD(FILENUM,0,"UP")
 S FILE=$$NUM2FILE^MSCV(FILENUM)
 I (FILENUM=0)!(FILE="")!('$$VFILE^DILFD(FILENUM)) D ADDPROP^MSCVRES(.REPLY,"ERROR","Could not find file.") D ENDCMPD^MSCVRES(.REPLY) Q
 D DOFIELDS(.REPLY,FILENUM,SQL,FILE,"")
 Q
DOFIELDS(REPLY,FILENUM,SQL,FILE,UPFILE,UPFIELD)	;
 N TYPE,P2,FLDNUM,FDTARGET,SQLTMP,SQLTMP2,DESC,I
 S:$G(FILE)="" FILE=$$NUM2FILE^MSCV(FILENUM)
 Q:FILE=""  ; Garbage? Like 1.008?
 D ADDCMPD^MSCVRES(.REPLY,"FILE")
 D ADDPROP^MSCVRES(.REPLY,"NAME",FILE)
 D ADDPROP^MSCVRES(.REPLY,"NUMBER",FILENUM)
 D ADDPROP^MSCVRES(.REPLY,"LOCATION",$$ROOT^DILFD(FILENUM))
 I UPFILE="" D
 . D FILE^DID(FILENUM,,"DESCRIPTION","DESC")
 E  D
 . D FIELD^DID(UPFILE,UPFIELD,,"DESCRIPTION","DESC")
 I $D(DESC("DESCRIPTION",1)) D
 . D ADDCMPD^MSCVRES(.REPLY,"DESC")
 . S I=0 F  S I=$O(DESC("DESCRIPTION",I)) Q:I=""  D ADDPROP^MSCVRES(.REPLY,I,DESC("DESCRIPTION",I)) W "=>"_DESC("DESCRIPTION",I),!
 . D ENDCMPD^MSCVRES(.REPLY) ; "DESC"
 I SQL D
 . S SQLTMP=$O(^DMSQ("T","C",FILENUM,""))
 . Q:SQLTMP=""
 . D ADDPROP^MSCVRES(.REPLY,"SQL",$P(^DMSQ("T",SQLTMP,0),"^",1))
 S FLDNUM=0
 F  S FLDNUM=$O(^DD(FILENUM,FLDNUM)) Q:FLDNUM'=+FLDNUM  D
 . D FIELD^DID(FILENUM,FLDNUM,,"LABEL;TYPE;GLOBAL SUBSCRIPT LOCATION","FDTARGET")
 . Q:FDTARGET("LABEL")=""  ; Garbage? File 604, field 18.1
 . D ADDCMPD^MSCVRES(.REPLY,"F")
 . D ADDPROP^MSCVRES(.REPLY,"N",FDTARGET("LABEL"))
 . S P2=$P(^DD(FILENUM,FLDNUM,0),"^",2)
 . S TYPE=$$TYPENUM(FDTARGET("TYPE"))
 . I (+P2)&(TYPE'=5) S TYPE=9 ; Multiples are subfile when not WP
 . D ADDPROP^MSCVRES(.REPLY,"T",TYPE)
 . D ADDPROP^MSCVRES(.REPLY,"L",FDTARGET("GLOBAL SUBSCRIPT LOCATION"))
 . D ADDPROP^MSCVRES(.REPLY,"#",FLDNUM)
 . I (TYPE=7) D
 . . D ADDPROP^MSCVRES(.REPLY,"P",+$P(P2,"P",2))
 . I (TYPE=9) D
 . . N SFNUM S SFNUM=+$P(^DD(FILENUM,FLDNUM,0),"^",2)
 . . Q:'SFNUM
 . . D DOFIELDS(.REPLY,SFNUM,SQL,"",FILENUM,FLDNUM)
 . E  I SQL D
 . . S SQLTMP=$O(^DMSQ("C","D",FILENUM,FLDNUM,""))
 . . Q:SQLTMP=""
 . . S SQLTMP2=$P(^DMSQ("C",SQLTMP,0),"^",1)
 . . D ADDPROP^MSCVRES(.REPLY,"Q",$P(^DMSQ("E",SQLTMP2,0),"^",1))
 . D ENDCMPD^MSCVRES(.REPLY) ; F
 . Q
 D ENDCMPD^MSCVRES(.REPLY) ; FILE
 Q
TYPENUM(TYPE)	;
 I '$D(^MSCTYPES) D
 . S ^MSCTYPES("DATE/TIME")=1
 . S ^MSCTYPES("NUMERIC")=2
 . S ^MSCTYPES("SET")=3
 . S ^MSCTYPES("FREE TEXT")=4
 . S ^MSCTYPES("MUMPS")=4
 . S ^MSCTYPES("WORD-PROCESSING")=5
 . S ^MSCTYPES("COMPUTED")=6
 . S ^MSCTYPES("POINTER")=7
 . S ^MSCTYPES("VARIABLE-POINTER")=8
 Q ^MSCTYPES(TYPE)
FILES(REPLY,RES)	;
 N IDX,FILENAME
 S IDX=1
 D ADDCMPD^MSCVRES(.REPLY,"RESULTS")
 F  S IDX=$O(^DD(IDX)) Q:IDX'=+IDX  D
 . Q:'$$VFILE^DILFD(IDX)  ; WP fields
 . S FILENAME=$$NUM2FILE^MSCV(IDX)
 . Q:FILENAME=""  ; Garbage in the structure? Like .008 or .3
 . D ADDCMPD^MSCVRES(.REPLY,"R")
 . D ADDPROP^MSCVRES(.REPLY,"T",FILENAME)
 . D ADDPROP^MSCVRES(.REPLY,"N",IDX)
 . D:$G(^DD(IDX,0,"UP")) ADDPROP^MSCVRES(.REPLY,"P",$G(^DD(IDX,0,"UP")))
 . D ENDCMPD^MSCVRES(.REPLY) ; R
 D ENDCMPD^MSCVRES(.REPLY) ; RESULTS
 Q
