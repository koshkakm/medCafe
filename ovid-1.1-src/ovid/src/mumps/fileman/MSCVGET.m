MSCVGET	; MSC/JDA - OVID FileMan GETS ; SEP 28, 2010 22:00:00
 ;;1.0;OpenVista Interface Domain;**1500**;May 14, 2009
 ;
RUN(REPLY,RES)	;
 N PROPNAME,FILE,IENS,FIELDS,TARGET,NAMES,FLAGS,NEWFLD
 S FIELDS="",FLAGS="N"
 ; Parse out the command properties
 F  Q:'$$NEXTPROP^MSCVRES(.RES)  D
 . S PROPNAME=$$GETPROP^MSCVRES(.RES)
 . I PROPNAME="FILE" S FILE=$$FILE2NUM^MSCV($$GETVAL^MSCVRES(.RES)) W FILE,! Q
 . I PROPNAME="IENS" S IENS=$$GETVAL^MSCVRES(.RES) Q
 . I PROPNAME="FIELD" S NEWFLD=$$PROCFLD(.RES,FILE,.NAMES) I NEWFLD'="" D  Q
 . . S:FIELDS'="" FIELDS=FIELDS_";" S FIELDS=FIELDS_NEWFLD
 . . Q
 . I PROPNAME="INTERNAL" S:$$GETVAL^MSCVRES(.RES) FLAGS=FLAGS_"I" Q
 . Q
 D GETS^DIQ(FILE,IENS,FIELDS,FLAGS,"TARGET","MSG")
 N IDX,VALUE,WPROW,SFCOUNT
 D ADDCMPD^MSCVRES(.REPLY,"RESULTS")
 S IDX=""
 F  S IDX=$O(TARGET(FILE,IENS,IDX)) Q:IDX=""  D
 . D ADDCMPD^MSCVRES(.REPLY,"FIELD")
 . D ADDPROP^MSCVRES(.REPLY,"NAME",NAMES(IDX))
 . I $D(TARGET(FILE,IENS,IDX,1))=1 D  ; Word processing field
 . . F WPROW=1:1 Q:$D(TARGET(FILE,IENS,IDX,WPROW))=0  D
 . . . D ADDPROP^MSCVRES(.REPLY,"VALUE",TARGET(FILE,IENS,IDX,WPROW))
 . . . Q
 . . Q
 . E  I $D(NAMES(IDX,"TYPE")) D  ; Special type - variable pointer
 . . S VALUE=$G(TARGET(FILE,IENS,IDX,"I"))
 . . ; Convert root to file number pointer to and return that
 . . S $P(VALUE,";",2)=+$P(@("^"_$P(VALUE,";",2)_"0)"),"^",2)
 . . D ADDPROP^MSCVRES(.REPLY,"VALUE",VALUE)
 . . Q
 . E  D
 . . S VALUE=$G(TARGET(FILE,IENS,IDX,"I"))
 . . S:VALUE="" VALUE=TARGET(FILE,IENS,IDX)
 . . D ADDPROP^MSCVRES(.REPLY,"VALUE",VALUE)
 . . Q
 . D ENDCMPD^MSCVRES(.REPLY) ; FIELD
 . Q
 S IDX="" F  S IDX=$O(NAMES(IDX)) Q:IDX=""  D
 . Q:'$D(NAMES(IDX,"SUB"))
 . K TARGET,MSG
 . D LIST^DIC(NAMES(IDX,"SUB"),","_IENS,"@","Q",,,,,,,"TARGET","MSG")
 . S SFCOUNT=+$O(TARGET("DILIST",2,""),-1)
 . Q:'SFCOUNT
 . D ADDCMPD^MSCVRES(.REPLY,"FIELD")
 . D ADDPROP^MSCVRES(.REPLY,"NAME",NAMES(IDX))
 . D ADDPROP^MSCVRES(.REPLY,"VALUE",SFCOUNT)
 . D ENDCMPD^MSCVRES(.REPLY) ; FIELD
 . Q
 D ENDCMPD^MSCVRES(.REPLY) ; RESULTS
 Q
PROCFLD(RES,FILE,NAMES)	;
 N PROPNAME,NAME,TYPE,NUM,SFNUM
 F  Q:'$$NEXTPROP^MSCVRES(.RES)  D
 . S PROPNAME=$$GETPROP^MSCVRES(.RES)
 . I PROPNAME="NAME" S NAME=$$GETVAL^MSCVRES(.RES) Q
 . I PROPNAME="TYPE" S TYPE=$$GETVAL^MSCVRES(.RES) Q
 . Q
 S NUM=$$FLD2NUM^MSCV(FILE,NAME)
 I 'NUM Q ""
 S NAMES(NUM)=NAME
 S SFNUM=$$GETSFNUM(FILE,NUM)
 I SFNUM S NAMES(NUM,"SUB")=SFNUM Q ""
 S:$D(TYPE) NAMES(NUM,"TYPE")=TYPE
 Q NUM
GETSFNUM(FILE,MFIELD)	; get subfile #
 ; get subfile for the given file and multiple-valued field
 N ATTR,SUBFILE
 S SUBFILE=""
 D FIELD^DID(FILE,MFIELD,"","MULTIPLE-VALUED;SPECIFIER;TYPE","ATTR")
 I ATTR("MULTIPLE-VALUED")=1,ATTR("TYPE")'="WORD-PROCESSING" D
 . S SUBFILE=+$G(ATTR("SPECIFIER"))
 . Q
 Q SUBFILE
