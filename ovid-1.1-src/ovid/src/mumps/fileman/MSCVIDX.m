MSCVIDX	; MSC/JDA - OVID FileMan INDEX ; SEP 28, 2010 22:00:00
 ;;1.0;OpenVista Interface Domain;**1500**;May 14, 2009
 ;
RUN(REPLY,RES)	;
 N FILENUM,PROPNAME,IDXNAME,FIELD,FILE
 F  Q:'$$NEXTPROP^MSCVRES(.RES)  D
 . S PROPNAME=$$GETPROP^MSCVRES(.RES)
 . I PROPNAME="FILE" S FILENUM=$$GETVAL^MSCVRES(.RES) Q
 . Q
 S IDXNAME=""
 ; old style indexes
 F  S IDXNAME=$O(^DD(FILENUM,0,"IX",IDXNAME)) Q:IDXNAME=""  D
 . D ADDCMPD^MSCVRES(.REPLY,"INDEX")
 . D ADDPROP^MSCVRES(.REPLY,"NAME",IDXNAME)
 . S FILE=$O(^DD(FILENUM,0,"IX",IDXNAME,""))
 . D ADDCMPD^MSCVRES(.REPLY,"FIELD")
 . D ADDPROP^MSCVRES(.REPLY,"FILE",FILE)
 . S FIELD=$O(^DD(FILENUM,0,"IX",IDXNAME,FILE,""))
 . D ADDPROP^MSCVRES(.REPLY,"FIELD",FIELD)
 . D ENDCMPD^MSCVRES(.REPLY) ; FIELD
 . D ENDCMPD^MSCVRES(.REPLY) ; INDEX
 . Q
 ; new style indexes
 N SCREEN,TRG,IDXNUM,TRG2,SUB,MSG,IENS
 S SCREEN="I $P(^DD(""IX"",Y,0),""^"",14)'=""A"""
 D FIND^DIC(.11,,"@;.11;.02;","QX",FILENUM,,"B",SCREEN,,"TRG")
 S IDXNUM="" F  S IDXNUM=$O(TRG("DILIST","ID",IDXNUM)) Q:IDXNUM=""  D
 . D ADDCMPD^MSCVRES(.REPLY,"INDEX")
 . D ADDPROP^MSCVRES(.REPLY,"STYLE","New")
 . D ADDPROP^MSCVRES(.REPLY,"NAME",TRG("DILIST","ID",IDXNUM,.02))
 . D ADDPROP^MSCVRES(.REPLY,"SHORTDESC",TRG("DILIST","ID",IDXNUM,.11))
 . S IENS=TRG("DILIST",2,IDXNUM)_","
 . N DESC I $$GET1^DIQ(.11,IENS,.1,,"DESC","MSG")'="" D
 . . N TMP S TMP=""
 . . F  S TMP=$O(DESC(TMP)) Q:TMP=""  D ADDPROP^MSCVRES(.REPLY,"LONGDESC",DESC(TMP))
 . . Q
 . D LIST^DIC(.114,","_IENS,"@;.01;2;3","Q",,,,,,,"TRG2")
 . S SUB="" F  S SUB=$O(TRG2("DILIST","ID",SUB)) Q:SUB=""  D
 . . D ADDCMPD^MSCVRES(.REPLY,"FIELD")
 . . D ADDPROP^MSCVRES(.REPLY,"ORDER",TRG2("DILIST","ID",SUB,.01))
 . . D ADDPROP^MSCVRES(.REPLY,"FILE",TRG2("DILIST","ID",SUB,2))
 . . D ADDPROP^MSCVRES(.REPLY,"FIELD",TRG2("DILIST","ID",SUB,3))
 . . D ENDCMPD^MSCVRES(.REPLY) ; FIELD
 . . Q
 . D ENDCMPD^MSCVRES(.REPLY) ; INDEX
 . Q
 Q
