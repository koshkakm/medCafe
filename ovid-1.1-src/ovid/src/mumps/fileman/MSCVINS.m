MSCVINS	; MSC/JDA - OVID FileMan INSERT ; SEP 28, 2010 22:00:00
 ;;1.0;OpenVista Interface Domain;**1500**;May 14, 2009
 ;
RUN(REPLY,RES)	;
 N PROPNAME,FILE,INPUTFDA,IEN,SEQ,IDX,MSG,FDA,WPDATA
 ; Parse out the command properties
 F  Q:'$$NEXTPROP^MSCVRES(.RES)  D
 . S PROPNAME=$$GETPROP^MSCVRES(.RES)
 . I PROPNAME="FIELD" D PROCFLD(.RES,FILE,.INPUTFDA,.FDA,"WPDATA")
 . I PROPNAME="FILE" S FILE=$$FILE2NUM^MSCV($$GETVAL^MSCVRES(.RES)) Q
 . Q
 D VALS^DIE(,"INPUTFDA","FDA","MSG")
 Q:$$ERROR(.MSG)
 D UPDATE^DIE(,"FDA","IEN","MSG")
 Q:$$ERROR(.MSG)
 D ADDCMPD^MSCVRES(.REPLY,"RESULTS")
 S SEQ=""
 F  S SEQ=$O(IEN(SEQ)) Q:SEQ=""  D
 . D ADDCMPD^MSCVRES(.REPLY,"IEN")
 . D ADDPROP^MSCVRES(.REPLY,"VALUE",IEN(SEQ))
 . D ADDPROP^MSCVRES(.REPLY,"SEQ",SEQ)
 . D ENDCMPD^MSCVRES(.REPLY) ; IEN
 . Q
 D ENDCMPD^MSCVRES(.REPLY) ; RESULTS
 Q
PROCFLD(RES,FILE,INPUTFDA,FDA,WPVAR)	;
 N NAME,VALUE,IENS,INTERN,WPLINES,WPNUM
 S IENS="+1,"
 S INTERN=0,WPNUM=1
 F  Q:'$$NEXTPROP^MSCVRES(.RES)  D
 . S PROPNAME=$$GETPROP^MSCVRES(.RES)
 . I PROPNAME="NAME" S NAME=$$FLD2NUM^MSCV(FILE,$$GETVAL^MSCVRES(.RES)) Q
 . I PROPNAME="VALUE" S VALUE=$$GETVAL^MSCVRES(.RES) Q
 . I PROPNAME="WPLINE" S WPLINES(WPNUM)=$$GETVAL^MSCVRES(.RES),WPNUM=WPNUM+1 Q
 . I PROPNAME="INTERNAL" S INTERN=$$GETVAL^MSCVRES(.RES) Q
 . I PROPNAME="IENS" S IENS=$$GETVAL^MSCVRES(.RES) Q
 . Q
 I $D(WPLINES) S FDA(FILE,IENS,NAME)=WPVAR_"("""_NAME_""")" M @FDA(FILE,IENS,NAME)=WPLINES Q
 I INTERN=0 S INPUTFDA(FILE,IENS,NAME)=VALUE Q
 S FDA(FILE,IENS,NAME)=VALUE
 Q
ERROR(MSG)	;
 Q:$G(MSG("DIERR"))="" 0
 N NUM
 D ADDCMPD^MSCVRES(.REPLY,"ERRORS")
 F IDX=1:1 Q:$G(MSG("DIERR",IDX))=""  D
 . S NUM=""
 . D ADDCMPD^MSCVRES(.REPLY,"ERROR")
 . S NUM=$G(MSG("DIERR",IDX,"PARAM","FIELD"))
 . S:NUM="" NUM=$G(MSG("DIERR",IDX,"PARAM","FILE"))
 . D ADDPROP^MSCVRES(.REPLY,"NUMBER",NUM)
 . D ADDPROP^MSCVRES(.REPLY,"TEXT",MSG("DIERR",IDX,"TEXT",1))
 . D ENDCMPD^MSCVRES(.REPLY) ; FIELD
 D ENDCMPD^MSCVRES(.REPLY) ; ERRORS
 Q 1
