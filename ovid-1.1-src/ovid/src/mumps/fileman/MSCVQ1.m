MSCVQ1	; MSC/JDA - RPC for FileMan requests ; SEP 28, 2010 22:00:00
 ;;1.0;OpenVista Interface Domain;**1500**;Apr 27, 2010
 ;
QIEN(REPLY,RES)	; Handle general-purpose query
 N FILE,FIELDS,IENSS,PROPNAME
 F  Q:'$$NEXTPROP^MSCVRES(.RES)  D
 .S PROPNAME=$$GETPROP^MSCVRES(.RES)
 .I PROPNAME="FIELD" D PROCFLD(.RES,.FIELDS) Q
 .I PROPNAME="IENS" S IENSS($$GETVAL^MSCVRES(.RES))="" Q
 .I PROPNAME="FILE" S FILE=$$FILE2NUM^MSCV($$GETVAL^MSCVRES(.RES)) Q
 .Q
 ; Add check that we have all we need and send back error otherwise
 D ADDCMPD^MSCVRES(.REPLY,"RESULTS")
 N CURIENS,CURFLD,TARGET S CURIENS="",CURFLD=""
 F  S CURIENS=$O(IENSS(CURIENS)) Q:CURIENS=""  D
 .D ADDCMPD^MSCVRES(.REPLY,"RECORD")
 .D ADDPROP^MSCVRES(.REPLY,"IENS",CURIENS)
 .F  S CURFLD=$O(FIELDS(CURFLD)) Q:CURFLD=""  D
 ..K TARGET N TARGET,IDX
 ..D ADDCMPD^MSCVRES(.REPLY,"FIELD")
 ..D ADDPROP^MSCVRES(.REPLY,"NAME",FIELDS(CURFLD,"NAME"))
 ..S VALUE=$$GET1^DIQ(FILE,CURIENS,CURFLD,FIELDS(CURFLD,"FLAGS"),"TARGET")
 ..I $D(TARGET) D
 ...S IDX="" F  S IDX=$O(TARGET(IDX)) Q:IDX=""  D ADDPROP^MSCVRES(.REPLY,"VALUE",TARGET(IDX))
 ...Q
 ..E  D ADDPROP^MSCVRES(.REPLY,"VALUE",VALUE)
 ..D ENDCMPD^MSCVRES(.REPLY) ; FIELD
 ..Q
 .D ENDCMPD^MSCVRES(.REPLY) ; RECORD
 .Q
 D ENDCMPD^MSCVRES(.REPLY) ; RESULTS
 Q
PROCFLD(RES,FIELDS)	;
 N PROPNAME,NAME,INTERNAL,NUM
 S INTERNAL=""
 F  Q:'$$NEXTPROP^MSCVRES(.RES)  D
 .S PROPNAME=$$GETPROP^MSCVRES(.RES)
 .I PROPNAME="NAME" S NAME=$$GETVAL^MSCVRES(.RES) Q
 .I PROPNAME="INTERNAL" S INTERNAL=$$GETVAL^MSCVRES(.RES) Q
 .Q
 S NUM=$$FLD2NUM^MSCV(FILE,NAME)
 S FIELDS(NUM,"FLAGS")=INTERNAL
 S FIELDS(NUM,"NAME")=NAME
 Q
TEST	;
 N QUERY,REPLY
 D START^MSCVRES(.QUERY)
 D START^MSCVRES(.REPLY)
 D ADDCMPD^MSCVRES(.QUERY,"QIEN")
 D ADDPROP^MSCVRES(.QUERY,"FILE","REMOTE PROCEDURE")
 D ADDPROP^MSCVRES(.QUERY,"IENS","1,")
 D ADDPROP^MSCVRES(.QUERY,"IENS","2,")
 D ADDCMPD^MSCVRES(.QUERY,"FIELD")
 D ADDPROP^MSCVRES(.QUERY,"NAME","NAME")
 D ENDCMPD^MSCVRES(.QUERY) ; FIELD
 D ADDCMPD^MSCVRES(.QUERY,"FIELD")
 D ADDPROP^MSCVRES(.QUERY,"NAME","DESCRIPTION")
 D ENDCMPD^MSCVRES(.QUERY) ; FIELD
 D ENDCMPD^MSCVRES(.QUERY) ; QUERY
 D FIRST^MSCVRES(.QUERY)
 D DUMP^MSCVRES(.QUERY,"")
 D FIRST^MSCVRES(.QUERY)
 D QIEN(.REPLY,.QUERY)
 D FIRST^MSCVRES(.REPLY)
 D DUMP^MSCVRES(.REPLY,"")
 D DISPOSE^MSCVRES(.QUERY)
 D DISPOSE^MSCVRES(.REPLY)
 Q
