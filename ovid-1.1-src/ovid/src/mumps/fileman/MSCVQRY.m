MSCVQRY	; MSC/JDA - OVID FileMan base for LIST and FIND ; SEP 28, 2010 22:00:00
 ;;1.0;OpenVista Interface Domain;**1500**;May 14, 2009
 ;
BLDREPLY(THIS,TARGET,REPLY)	;
 I THIS("FROM")'="" D
 . D ADDCMPD^MSCVRES(.REPLY,"FROM")
 . N FROM M FROM=THIS("FROM")
 . D SCAN^MSCVDIC(.REPLY,"FROM",0)
 . D ENDCMPD^MSCVRES(.REPLY) ; FROM
 . Q
 D ADDCMPD^MSCVRES(.REPLY,"RESULTS")
 ; Check for HTML encoded packed output
 I THIS("PACK")&($P(TARGET("DILIST",0),"^",4)["H") D ADDPROP^MSCVRES(.REPLY,"HTML",1)
 N MAP,MAPFLD,TYPEMAP,FNUM,PC
 I THIS("PACK") D
 . S MAP=TARGET("DILIST",0,"MAP")
 . ; Translate the field numbers back into the field names we were given
 . ; Also, remap special types from field #s to positions
 . F FNUM=1:1 S MAPFLD=$P(MAP,"^",FNUM) Q:MAPFLD=""  D
 . . Q:'$D(THIS("NAMES",MAPFLD))
 . . S $P(MAP,"^",FNUM)=THIS("NAMES",MAPFLD)
 . . S:$D(THIS("TYPES",MAPFLD)) TYPEMAP(FNUM)=THIS("TYPES",MAPFLD)
 . . Q
 . Q
 E  D
 . S MAPFLD=""
 . F  S MAPFLD=$O(THIS("NAMES",MAPFLD)) Q:MAPFLD=""  D
 . . S:$D(THIS("TYPES",MAPFLD)) TYPEMAP(THIS("NAMES",MAPFLD))=THIS("TYPES",MAPFLD)
 . . Q
 . Q
 I $D(MAP) D
 . D ADDCMPD^MSCVRES(.REPLY,"MAP")
 . F PC=1:1:$L(MAP,"^") D ADDPROP^MSCVRES(.REPLY,"M",$P(MAP,"^",PC))
 . D ENDCMPD^MSCVRES(.REPLY) ; MAP
 . Q
 N SEQNUM,NODE,IDX,VALUE,FLDVAL
 ; Loop though results and create reply resource
 I THIS("PACK") D  ; Packed format
 . S SEQNUM=0
 . I $D(THIS("TYPES")) D  ; need to translate variable pointers to return file #s
 . . F  S SEQNUM=$O(TARGET("DILIST",SEQNUM)) Q:SEQNUM=""  D
 . . . S VALUE=TARGET("DILIST",SEQNUM,0)
 . . . S IDX=""
 . . . F  S IDX=$O(TYPEMAP(IDX)) Q:IDX=""  D
 . . . . S FLDVAL=$P(VALUE,"^",IDX)
 . . . . I FLDVAL'="" D
 . . . . . S $P(FLDVAL,";",2)=+$P(@("^"_$P(FLDVAL,";",2)_"0)"),"^",2)
 . . . . . S $P(VALUE,"^",IDX)=FLDVAL
 . . . . . Q
 . . . . Q
 . . . D ADDPROP^MSCVRES(.REPLY,"RD",VALUE)
 . . . Q
 . . Q
 . E  D
 . . F  S SEQNUM=$O(TARGET("DILIST",SEQNUM)) Q:SEQNUM=""  D
 . . . D ADDPROP^MSCVRES(.REPLY,"RD",TARGET("DILIST",SEQNUM,0))
 . . . Q
 . . Q
 . Q
 E  D  ; Unpacked data
 . N FLDNUM
 . S SEQNUM="0"
 . F  S SEQNUM=$O(TARGET("DILIST","ID",SEQNUM)) Q:SEQNUM=""  D
 . . D ADDCMPD^MSCVRES(.REPLY,"ROW")
 . . ; Add IEN field
 . . D ADDCMPD^MSCVRES(.REPLY,"FIELD")
 . . D ADDPROP^MSCVRES(.REPLY,"NAME","IEN")
 . . D ADDPROP^MSCVRES(.REPLY,"VALUE",TARGET("DILIST",2,SEQNUM))
 . . D ENDCMPD^MSCVRES(.REPLY) ; FIELD
 . . ; Loop through fields
 . . S FLDNUM=""
 . . F  S FLDNUM=$O(TARGET("DILIST","ID",SEQNUM,FLDNUM)) Q:FLDNUM=""  D
 . . . S VALUE=TARGET("DILIST","ID",SEQNUM,FLDNUM)
 . . . Q:VALUE=""  ; Do not report empty nodes
 . . . S:$D(TYPEMAP(FLDNUM)) $P(VALUE,";",2)=+$P(@("^"_$P(VALUE,";",2)_"0)"),"^",2)
 . . . D ADDCMPD^MSCVRES(.REPLY,"FIELD")
 . . . D ADDPROP^MSCVRES(.REPLY,"NAME",THIS("NAMES",FLDNUM))
 . . . D ADDPROP^MSCVRES(.REPLY,"VALUE",VALUE)
 . . . D ENDCMPD^MSCVRES(.REPLY) ; FIELD
 . . . Q
 . . D ENDCMPD^MSCVRES(.REPLY) ; ROW
 . . Q
 . Q
 D ENDCMPD^MSCVRES(.REPLY) ; RESULTS
 Q
CNSTRCT(THIS)	;
 S THIS("PACK")=0,THIS("NUMBER")="*",THIS("FLAGS")="Q",THIS("FIELDS")="@"
 S THIS("IENS")="",THIS("FROM")="",THIS("SCREEN")=""
 Q
PROCPROP(THIS,RES)	;
 N FROM
 S PROPNAME=$$GETPROP^MSCVRES(.RES)
 I PROPNAME="FIELD" D  Q
 . N TMP
 . S TMP=$$PROCFLD(.THIS,.RES)
 . Q:'TMP  ; obsolete or multiple field
 . S THIS("FIELDS")=THIS("FIELDS")_";"_TMP
 . Q
 I PROPNAME="FILE" S THIS("FILE")=$$FILE2NUM^MSCV($$GETVAL^MSCVRES(.RES)) S:THIS("FILE")=0 THIS("ERROR")="Could not find file '"_$$GETVAL^MSCVRES(.RES)_"'." Q
 I PROPNAME="NUMBER" S THIS("NUMBER")=+$$GETVAL^MSCVRES(.RES) Q
 I PROPNAME="FROM" D READ^MSCVDIC(.RES,.FROM) M THIS("FROM")=FROM Q
 I PROPNAME="IENS" S THIS("IENS")=$$GETVAL^MSCVRES(.RES) Q
 I PROPNAME="PACK" S THIS("PACK")=$$GETVAL^MSCVRES(.RES) S:THIS("PACK") THIS("FLAGS")=THIS("FLAGS")_"P" Q
 I PROPNAME="SCREEN" S THIS("SCREEN")="I "_$$PROCESS^MSCVSCN(.THIS,.RES) Q
 I PROPNAME="SAFE" S THIS("SAFE")=$$GETVAL^MSCVRES(.RES) Q
 Q
PROCFLD(THIS,RES)	;
 N NAME,PROPNAME,FLAGS,INTERN,EXTERN,NUM,TYPE,TARGET
 S NAME="",INTERN="0",EXTERN="0"
 F  Q:'$$NEXTPROP^MSCVRES(.RES)  D
 . S PROPNAME=$$GETPROP^MSCVRES(.RES)
 . I PROPNAME="NAME" S NAME=$$GETVAL^MSCVRES(.RES) Q
 . I PROPNAME="INTERNAL" S INTERN=$$GETVAL^MSCVRES(.RES) Q
 . I PROPNAME="TYPE" S TYPE=$$GETVAL^MSCVRES(.RES) Q
 . Q
 S NUM=$$FLD2NUM^MSCV(THIS("FILE"),NAME)
 Q:'NUM 0 ; obsolete field
 D FIELD^DID(THIS("FILE"),NUM,,"MULTIPLE-VALUED","TARGET")
 Q:'$D(TARGET("MULTIPLE-VALUED")) 0
 Q:TARGET("MULTIPLE-VALUED") 0
 S THIS("NAMES",NUM)=NAME ; for unpacked
 S:INTERN NUM=NUM_"I"
 S:EXTERN NUM=NUM_"E"
 S THIS("NAMES",NUM)=NAME ; for packed
 S:$D(TYPE) THIS("TYPES",NUM)=TYPE
 Q NUM
