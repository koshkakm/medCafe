MSCVREF	; MSC/JDA - OVID FileMan REFBY ; SEP 28, 2010 22:00:00
 ;;1.0;OpenVista Interface Domain;**1500**;May 14, 2009
 ;
RUN(REPLY,RES)	;
 N FILENUM,PROPNAME,VAR,FILE,FIELD,FILENAME
 S FILENUM=0,FILENAME=""
 F  Q:'$$NEXTPROP^MSCVRES(.RES)  D
 . S PROPNAME=$$GETPROP^MSCVRES(.RES)
 . I PROPNAME="FILE" S FILENAME=$$GETVAL^MSCVRES(.RES) Q
 . Q
 S:FILENAME'="" FILENUM=$$FILE2NUM^MSCV(FILENAME)
 I FILENUM=0 D ADDPROP^MSCVRES(.REPLY,"ERROR","Could not find file.") Q
 S VAR="^DD("_FILENUM_",0,""PT"")"
 F  S VAR=$Q(@VAR) Q:($QS(VAR,1)'=FILENUM)!($QS(VAR,3)'="PT")  D
 . S FILE=$QS(VAR,4)
 . S FIELD=$QS(VAR,5)
 . D ADDCMPD^MSCVRES(.REPLY,"R")
 . D ADDPROP^MSCVRES(.REPLY,"F",$$NUM2FILE^MSCV(FILE))
 . D ADDPROP^MSCVRES(.REPLY,"F#",FILE)
 . D ADDPROP^MSCVRES(.REPLY,"C",$P(^DD(FILE,FIELD,0),"^",1))
 . D ADDPROP^MSCVRES(.REPLY,"C#",FIELD)
 . D ENDCMPD^MSCVRES(.REPLY) ; R
 . Q
 Q
