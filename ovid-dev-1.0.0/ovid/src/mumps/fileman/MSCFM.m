MSCFM; MSC/JDA - Common OVID FileMan routines ; OCT 20, 2009
 ;;1.0;MSCFM;;May 14, 2009
 Q
PROCESS(REPLY,RES)
 N TEMP,PROPNAME
 D FIRST^MSCRES(.RES)
 D START^MSCRES(.REPLY)
 D ADDCMPD^MSCRES(.REPLY,"FILEMAN")
 S TEMP=$$NEXTPROP^MSCRES(.RES)
 ; JDA - TODO : Check for valid top-property
 F  Q:'$$NEXTPROP^MSCRES(.RES)  D
 . S PROPNAME=$$GETPROP^MSCRES(.RES)
 . I PROPNAME="GETS" D RUN^MSCFMGET(.REPLY,.RES) Q
 . I PROPNAME="LIST" D RUN^MSCFMLST(.REPLY,.RES) Q
 . I PROPNAME="FIND" D RUN^MSCFMFND(.REPLY,.RES) Q
 . I PROPNAME="INSERT" D RUN^MSCFMINS(.REPLY,.RES) Q
 . I PROPNAME="DELETE" D RUN^MSCFMDEL(.REPLY,.RES) Q
 . I PROPNAME="UPDATE" D RUN^MSCFMUPD(.REPLY,.RES) Q
 . I PROPNAME="FILEINFO" D FILEINFO^MSCFMFI(.REPLY,.RES) Q
 . I PROPNAME="FILES" D FILES^MSCFMFI(.REPLY,.RES) Q
 . I PROPNAME="PROJECT" D RPCPROJ^MSCSQL2(.REPLY,.RES) Q
 . I PROPNAME="DIC" D RUN^MSCFMDIC(.REPLY,.RES) Q
 . I PROPNAME="INDEX" D RUN^MSCFMIDX(.REPLY,.RES) Q
 . I PROPNAME="REFBY" D RUN^MSCFMREF(.REPLY,.RES) Q
 . Q
 D ENDCMPD^MSCRES(.REPLY) ; FILEMAN
 Q
FILE2NUM(FILE)
 N PC,PART,FLD
 S PART=$P(FILE,"^",1)
 S FILENUM=$$FIND1^DIC(1,,"X",$P(PART,"^",1))
 Q:FILENUM="" 0
 F PC=2:1:$L(FILE,"^") D
 . S PART=$P(FILE,"^",PC)
 . S FLD=$$FLD2NUM(FILENUM,PART)
 . S FILENUM=+$P(^DD(FILENUM,FLD,0),"^",2)
 . Q
 Q FILENUM
FLD2NUM(FILENUM,FLDNUM)
 I FLDNUM?1(.N0.1"I",.N1".".N0.1"I") S:'$D(^DD(FILENUM,FLDNUM)) FLDNUM=0 Q FLDNUM ; already a number, but verify it exists
 E  Q $$FLDNUM^DILFD(FILENUM,FLDNUM)
NUM2FILE(FILENUM)
 N FILENAME
 S FILENAME=$O(^DD(FILENUM,0,"NM",""))
 Q:('$D(^DIC(FILENUM,0))&($G(^DD(FILENUM,0,"UP"))="")) "" ; No File. Garbage? Like .3
 Q FILENAME
