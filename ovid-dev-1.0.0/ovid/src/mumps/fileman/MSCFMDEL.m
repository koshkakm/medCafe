MSCFMDEL; MSC/JDA - OVID FileMan DELETE ; May 14, 2009
 ;;1.0;MSCFM;;May 14, 2009
 Q
RUN(REPLY,RES)
 N PROPNAME,FILE,FDA,IENS,MSG,ERRNO
 ; Parse out the command properties
 F  Q:'$$NEXTPROP^MSCRES(.RES)  D
 . S PROPNAME=$$GETPROP^MSCRES(.RES)
 . I PROPNAME="FILE" S FILE=$$FILE2NUM^MSCFM($$GETVAL^MSCRES(.RES)) Q
 . I PROPNAME="IENS" S IENS=$$GETVAL^MSCRES(.RES) Q
 . Q
 S:$E(IENS,$L(IENS))'="," IENS=IENS_","
 S FDA(FILE,IENS,.01)="@"
 D FILE^DIE(,"FDA","MSG")
 D ADDCMPD^MSCRES(.REPLY,"RESULTS")
 F ERRNO=1:1 Q:'$G(MSG("DIERR",ERRNO))  D
 . D ADDCMPD^MSCRES(.REPLY,"ERROR")
 . D ADDPROP^MSCRES(.REPLY,"ERROR NUMBER",MSG("DIERR",ERRNO))
 . D ADDPROP^MSCRES(.REPLY,"ERROR TEXT",MSG("DIERR",ERRNO,"TEXT",1))
 . D ENDCMPD^MSCRES(.REPLY) ; ERROR
 . Q
 D ENDCMPD^MSCRES(.REPLY) ; RESULTS
 Q
