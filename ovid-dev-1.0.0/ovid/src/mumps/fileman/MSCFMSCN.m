MSCFMSCN; MSC/JDA - OVID FileMan screens ; May 14, 2009
 ;;1.0;MSCFM;;May 14, 2009
 Q
PROCESS(THIS,RES) ; Parse equation resource into text suitable for a SCREEN
 N RETVAL,PROPNAME S RETVAL=""
 F  Q:'$$NEXTPROP^MSCRES(.RES)  D  ; There should only be one, but loop anyway
 . S PROPNAME=$$GETPROP^MSCRES(.RES)
 . I PROPNAME="ISNULL" S RETVAL=$$PROCNULL(.THIS,.RES) Q
 . I PROPNAME="EQUALS" S RETVAL=$$PROCEQ(.THIS,.RES) Q
 . I PROPNAME="AND" S RETVAL=$$PROCAND(.THIS,.RES) Q
 . I PROPNAME="OR" S RETVAL=$$PROCOR(.THIS,.RES) Q
 . I PROPNAME="GT" S RETVAL=$$PROCGT(.THIS,.RES) Q
 . I PROPNAME="FIELD" S RETVAL=$$PROCFLD(.THIS,.RES) Q
 . I PROPNAME="VALUE" S RETVAL=$$PROCVAL(.THIS,.RES) Q
 . I PROPNAME="IEN" S RETVAL=$$PROCIEN(.THIS,.RES) Q
 . Q
 Q RETVAL
PROCEQ(THIS,RES) ; Check for equals
 N PROPNAME,LEFT,RIGHT
 F  Q:'$$NEXTPROP^MSCRES(.RES)  D
 . S PROPNAME=$$GETPROP^MSCRES(.RES)
 . I PROPNAME="LEFT" S LEFT=$$PROCESS(.THIS,.RES) Q
 . I PROPNAME="RIGHT" S RIGHT=$$PROCESS(.THIS,.RES) Q
 . Q
 Q "("_LEFT_"="_RIGHT_")"
PROCAND(THIS,RES) ; Boolean AND between two clauses
 N PROPNAME,LEFT,RIGHT
 F  Q:'$$NEXTPROP^MSCRES(.RES)  D
 . S PROPNAME=$$GETPROP^MSCRES(.RES)
 . I PROPNAME="LEFT" S LEFT=$$PROCESS(.THIS,.RES) Q
 . I PROPNAME="RIGHT" S RIGHT=$$PROCESS(.THIS,.RES) Q
 . Q
 Q "("_LEFT_"&"_RIGHT_")"
PROCOR(THIS,RES) ; Boolean OR between two clauses
 N PROPNAME,LEFT,RIGHT
 F  Q:'$$NEXTPROP^MSCRES(.RES)  D
 . S PROPNAME=$$GETPROP^MSCRES(.RES)
 . I PROPNAME="LEFT" S LEFT=$$PROCESS(.THIS,.RES) Q
 . I PROPNAME="RIGHT" S RIGHT=$$PROCESS(.THIS,.RES) Q
 . Q
 Q "("_LEFT_"!"_RIGHT_")"
PROCGT(THIS,RES) ; Greater Than between two clauses
 N PROPNAME,LEFT,RIGHT
 F  Q:'$$NEXTPROP^MSCRES(.RES)  D
 . S PROPNAME=$$GETPROP^MSCRES(.RES)
 . I PROPNAME="LEFT" S LEFT=$$PROCESS(.THIS,.RES) Q
 . I PROPNAME="RIGHT" S RIGHT=$$PROCESS(.THIS,.RES) Q
 . Q
 Q "("_LEFT_">"_RIGHT_")"
PROCNULL(THIS,RES) ; Check for null
 N RETVAL,FIELD,BOOL,PROPNAME S RETVAL="1",FIELD="",BOOL=1
 F  Q:'$$NEXTPROP^MSCRES(.RES)  D
 . S PROPNAME=$$GETPROP^MSCRES(.RES)
 . I PROPNAME="FIELD" S FIELD=$$PROCFLD(.THIS,.RES) Q
 . I PROPNAME="BOOL" S BOOL=$$GETVAL^MSCRES(.RES) Q
 . Q
 I (FIELD'="") D
 . S RETVAL=FIELD
 . S:'BOOL RETVAL=RETVAL_"'"
 . S RETVAL=RETVAL_"="""""
 . S RETVAL="("_RETVAL_")"
 . Q
 Q RETVAL
PROCVAL(THIS,RES) ; Constant value
 Q """"_$$GETVAL^MSCRES(.RES)_""""
PROCFLD(THIS,RES) ; Field in record
 ; Construct equation of form: $P($G(GLOBALROOT(Y,SUBSCRIPT)),"^",PART)
 N FIELD,GLOBNAME,TEMP,INDEX,LOC
 S FIELD=$$GETVAL^MSCRES(.RES)
 ; Get the raw global location and check that.
 D FILE^DID(THIS("FILE"),,"GLOBAL NAME","TEMP")
 S GLOBNAME=TEMP("GLOBAL NAME")
 D FIELD^DID(THIS("FILE"),FIELD,,"GLOBAL SUBSCRIPT LOCATION","TEMP")
 S INDEX=$P(TEMP("GLOBAL SUBSCRIPT LOCATION"),";",1)
 S LOC=$P(TEMP("GLOBAL SUBSCRIPT LOCATION"),";",2)
 Q "$P($G("_GLOBNAME_"Y,"""_INDEX_""")),""^"","_LOC_")"
PROCIEN(THIS,RES) ; IEN
 Q "Y"
