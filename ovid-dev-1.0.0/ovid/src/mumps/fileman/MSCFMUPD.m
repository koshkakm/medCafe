MSCFMUPD; MSC/JDA - OVID FileMan UPDATE ; May 14, 2009
 ;;1.0;MSCFM;;May 14, 2009
 Q
RUN(REPLY,RES)
 N PROPNAME,FILE,FDA,IENS,MSG,IDX,INPUTFDA
 ; Parse out the command properties
 F  Q:'$$NEXTPROP^MSCRES(.RES)  D
 . S PROPNAME=$$GETPROP^MSCRES(.RES)
 . I PROPNAME="FIELD" D PROCFLD(.RES,FILE,IENS,.INPUTFDA,.FDA)
 . I PROPNAME="FILE" S FILE=$$FILE2NUM^MSCFM($$GETVAL^MSCRES(.RES)) Q
 . I PROPNAME="IENS" S IENS=$$GETVAL^MSCRES(.RES) S:$E(IENS,$L(IENS))'="," IENS=IENS_"," Q
 . Q
 D VALS^DIE(,"INPUTFDA","FDA","MSG")
 I $G(MSG("DIERR"))'="" D  Q
 . D ADDCMPD^MSCRES(.REPLY,"ERRORS")
 . F IDX=1:1 Q:$G(MSG("DIERR",IDX))=""  D
 . . D ADDCMPD^MSCRES(.REPLY,"ERROR")
 . . D ADDPROP^MSCRES(.REPLY,"NUMBER",MSG("DIERR",IDX,"PARAM","FIELD"))
 . . D ADDPROP^MSCRES(.REPLY,"TEXT",MSG("DIERR",IDX,"TEXT",1))
 . . D ENDCMPD^MSCRES(.REPLY) ; ERROR
 . D ENDCMPD^MSCRES(.REPLY) ; ERRORS
 . Q
 D FILE^DIE(,"FDA","MSG")
 I $G(MSG("DIERR"))'="" D
 . D ADDCMPD^MSCRES(.REPLY,"ERRORS")
 . F IDX=1:1 Q:'$G(MSG("DIERR",IDX))  D
 . . D ADDCMPD^MSCRES(.REPLY,"ERROR")
 . . D ADDPROP^MSCRES(.REPLY,"NUMBER",MSG("DIERR",IDX))
 . . D ADDPROP^MSCRES(.REPLY,"TEXT",MSG("DIERR",IDX,"TEXT",1))
 . . D ENDCMPD^MSCRES(.REPLY) ; ERROR
 . . Q
 . D ENDCMPD^MSCRES(.REPLY) ; ERROR
 . Q
 E  D ADDCMPD^MSCRES(.REPLY,"RESULTS") D ENDCMPD^MSCRES(.REPLY) ; Empty results 
 Q
PROCFLD(RES,FILE,IENS,INPUTFDA,FDA)
 N NAME,VALUE,INTERN,RESULT
 S INTERN=0
 F  Q:'$$NEXTPROP^MSCRES(.RES)  D
 . S PROPNAME=$$GETPROP^MSCRES(.RES)
 . I PROPNAME="NAME" S NAME=$$GETVAL^MSCRES(.RES) Q  ; Either label or number is OK
 . I PROPNAME="VALUE" S VALUE=$$GETVAL^MSCRES(.RES) Q
 . I PROPNAME="INTERNAL" S INTERN=$$GETVAL^MSCRES(.RES) Q
 . Q
 I INTERN S INPUTFDA(FILE,IENS,NAME)=VALUE
 E  S FDA(FILE,IENS,NAME)=VALUE
 Q

